#Part of the attack routine is to call a Razor Dress routine named "bow".  
#   If you do not have that it will not stop the script but it is 
#   a good idea to have one in case you unequip weapon

#Target Lock:
#0=default behavior (Let the script try to be intelligent)
#1=Area discord -- kill one thing at a time (Good for things that do not want to spread damage.  BAD, like SUPER BAD, for poison
#2=Single target discord -- switch target after every round of skill usages (SPECIAL NOTE: Bosses will switch target type to this on purpose so you kill NPC spawns with bot and target disco boss)
@setvar targetLockType 0 

#Summon Type Selector.  Having Necromancy skill will still summon necro version.  We will always use Summon Creature as 5th slot.  Still requires 80 SS or Necro to trigger
#MAKE SURE you only select two here.  Selecting more will break stuff.  
#Valid Values are 0, 1, or 2 and it will summon that many of that type: Example setting fireElem to 2 will summon 2 fire elems.  Setting earthEleme to 1 and waterElem to 1 will summon 1 earth elemental and 1 waterElem
@setvar fireElem 2
@setvar earthElem 0
@setvar airElem 0
@setvar waterElem 0
@setvar daemonSum 0

#Repair Priority Queue.  Set these as 1/2/3.  If you fail to put one for each of these it will break shit.  Just leave it alone if you do not care
@setvar repairHullOrder 3
@setvar repairGunsOrder 2
@setvar repairSailsOrder 1

#Dynamic Boss Repair - Switches to hull priority on bosses.  Set to 1 to turn on.  Have to restart script to reset after bosses
@setvar dynamicBossRepair 1

#Set this to 1 if you do not  want to select your own tillerman (useful for recall or if you switch boats a lot.  Causes issues sometimes if it finds a friends dynamically)
@setvar dynamicTiller 0

#Set this to 1 if you want to throw self overboard if a ship repair kit is on the deck
@setvar triggerOverboard 2

#Set this to 1 to attempt to keep the bot from repairing within the combat repair window (only repairs 60-120s after boarding an NPC ship)
@setvar repairLockdown 0

#Set this to 1 if you want to keep up buffs for Reactive Armour and Magic Reflect
@setvar defensiveBuffs 0

#Set this to 1 if you want to simulate always being boarded on NPC ship for testing reasons
@setvar alwaysBoarded 0

#Set this to 0 if you want to keep my super awesome say commands (1 mutes the bot except important stuff like food or fighting party)
@setvar sayLess 0

#Set this to 1 if you want to be a super fucking awesome pirate
@setvar sayMore 0

#Set this to 1 if you want bot to defuse close by bombs and reembark
@setvar defuseBombs 1

#Set this to 1 to have bot reload right after embarking
@setvar reloadCannons 1

#Set this if you want to have bot recall on dock.  Must have a book in pack named Home and first rune be the house to recall to if not magery.  If magery make home the default rune
#IF YOU TURN THIS ON YOU MUST GO TO THE SECTION NAMED "WALK THIS WAY" AND SET UP DIRECTIONS INTO YOUR HOUSE FROM RECALL LOCATION
@setvar goHomeBotYouAreDrunk 1


##################################################################################
##################################################################################
##################################################################################
##############                                                 ###################
##############              No setup below here                ###################
##############                                                 ###################
##################################################################################
##################################################################################
##################################################################################

clearsysmsg 

if 'sayMore' = 1
    say "YO HO MATEY AWAY!" 33
    wait 500
endif

unsetvar 'tillerDude'
if dynamicTiller = 0
	overhead "Target Tillerman"
	@setvar tillerDude 
else
	#try to find tillerman
	if findtype "tiller man" ground as found
		overhead "Tillerman Set"
		setvar 'tillerDude' 'found'
		dclick 'tillerDude' 
		wait 500
	endif
endif

#Timers.  We used to do these on an if exists basis, but it fubard too much stuff if you leave client open forever
createtimer 'petAttack' 
settimer 'petAttack' 0
createtimer 'discoTimer'
settimer 'discoTimer' 5000
createtimer 'repairBoat'
settimer 'repairBoat' 8000
createtimer "shroomy"
settimer "shroomy" 61000
createtimer 'buffDisco'
settimer 'buffDisco' 300000
createtimer 'buffProvo'
settimer 'buffProvo' 300000
createtimer 'buffPeace'
settimer 'buffPeace' 300000
createtimer 'buffBeg'
settimer 'buffBeg' 300000 
createtimer 'bardLock'
settimer 'bardLock' 0
createtimer "aid time"
settimer "aid time" 8000
createtimer 'RepairHull'
settimer 'RepairHull' 0
createtimer 'buffTaste'
settimer 'buffTaste' 15000
createtimer 'newTargetCheck'
settimer 'newTargetCheck' 15000
createtimer 'spiritSpeakCheck'
settimer 'spiritSpeakCheck' 15000
createtimer 'foodCheck'
settimer 'foodCheck' 120000
createtimer 'crookCheck'
settimer 'crookCheck' 120000
createtimer 'crewHealing'
settimer 'crewHealing' 120000
createtimer 'onBoard'
settimer 'onBoard' 120000
createtimer 'painSpkeTimer'
settimer 'painSpkeTimer' 120000
createtimer 'poisonStrikeTimer'
settimer 'poisonStrikeTimer' 120000
createtimer 'vampTimer'
settimer 'vampTimer' 120000
createtimer 'dressTimer'
settimer 'dressTimer' 120000
createtimer 'provkeBossTimer' 
settimer 'provkeBossTimer' 1500000
createtimer 'goHomeBotYouAreDrunkTimer'
settimer 'goHomeBotYouAreDrunkTimer' 100000
createtimer 'scriptRunning'
settimer 'scriptRunning' 100000
createtimer 'singSeaShanties'
settimer 'singSeaShanties' 70000
createtimer 'sanityEmbarkCheck'
settimer 'sanityEmbarkCheck' 0

#Clear lastt t arget from last run - this can cause a weird bug
unsetvar 'lastserial' 

#Create list of instrument types and dclick if found
removelist 'instrumentTypes'
createlist 'instrumentTypes'
pushlist 'instrumentTypes' 10245
pushlist 'instrumentTypes' 3762
pushlist 'instrumentTypes' 3740
pushlist 'instrumentTypes' 3742
pushlist 'instrumentTypes' 3763

#Create list of reagents
createlist regTypes
clearlist regTypes
@pushlist 'regTypes' 'Blood Moss'
@pushlist 'regTypes' 'Black Pearl%s%'
@pushlist 'regTypes' 'Ginseng'
@pushlist 'regTypes' 'Garlic'
@pushlist 'regTypes' 'Sulfurous Ash'
@pushlist 'regTypes' "Spider's Silk" 
@pushlist 'regTypes' 'Nightshade' 
@pushlist 'regTypes' 'Mandrake Root%s%' 
# ' for formatting reasons

#Loop through instruments and dclick one
if skill "Musicianship" > 50
    foreach instrumentType in 'instrumentTypes'
        dclicktype instrumentType
    endfor
endif

#open necro book
if skill "Necromancy" > 50
    dclicktype "8787"
    waitforgump 3897999232
    gumpresponse 2 3897999232
    wait 250
    gumpclose 3897999232
endif   

#Set boarded state - 1=boarded NPC 0=on our ship
@setvar 'boardState' 0
#Set up tracking sate 1=tracking 0=not
@setvar 'trackState' 0
#set up var to check if we have enemies in range
@setvar 'closeEnemy' 0
#set up blue tracking option
@setvar 'blueSighted' 0
#Boss Tracker
@setvar 'bossFight' 0
#Target Lock
@setvar targetLock targetLockType
#discoLock
@setvar discoLock targetLockType
#provoke boss
@setvar 'provokeBoss' 0
#set bot at home flag
@setvar 'botAtHome' 0
#set where we are on wellerman
@setvar wellerManProgress 0

#set pets if they exist
if skill "Veterinary" > 50 and followers >= 1
    overhead "Target pet 1" 
    setvar! pet1
    overhead "Target pet 2"
    setvar! pet2
    overhead "Target pet 3"
    setvar! pet3 
endif   

if skill "Healing" > 50
    say '[ShipHotbars'
    wait 500
endif   

#do camping buff at least once regardless of buff status
#camping buff
if skill "Camping" > 50 
    if not findtype "kindling" backpack
        say "Need more kindling" 
        wait 355
        say "Need more kindling" 
        wait 355
        say "Need more kindling" 
        wait 355
    endif

    if not findtype "campfire" true
        dclicktype "kindling" backpack
        wait 350
        while insysmsg "You fail to ignite"
            wait 3000    
            dclicktype "kindling" backpack
            wait 350
        endwhile
    endif
    wait 600
endif

     
if followers > 1
    say "All guard me"
endif   


while not dead
	#Announce we are running
    if timer "scriptRunning" > 15000
        overhead "Bot is running"
        settimer "scriptRunning" 0
    endif
	
	#Dynamic Tillerman Check
	if dynamicTiller = 1
		if not varexist 'tillerDude'
			if findtype "tiller man" ground as found
				overhead "Tillerman Set"
				setvar 'tillerDude' 'found'
				dclick 'tillerDude'
				wait 500
			endif
		endif
	endif
    #put in a pause so we dont spam 
    wait 100    
    
    if alwaysBoarded = 1
        @setvar boardState 1
    endif
    
    #Be a real pirate
    if wellerManProgress = 0 and sayMore = 1 and timer "singSeaShanties" > 20000 and boardState = 0
        wait 500
        say "There once was a ship that put to sea / The name of the ship was the Billy of Tea " 45
        wait 500
        say "/ The winds blew up, her bow dipped down / Oh blow, my bully boys, blow" 45
        settimer 'singSeaShanties' 0
        @setvar wellerManProgress 1
    elseif wellerManProgress = 1 and sayMore = 1 and timer "singSeaShanties" > 20000 and boardState = 0
        wait 500
        say "Soon may the Wellerman come / To bring us sugar and tea and rum " 45
        wait 500
        say "/ One day, when the tonguing is done / Well take our leave and go"  45
        settimer 'singSeaShanties' 0
        @setvar wellerManProgress 2
    elseif wellerManProgress = 2 and sayMore = 1 and timer "singSeaShanties" > 20000 and boardState = 0
        wait 500
        say "Shed not been two weeks from shore / When down on her a right whale bore " 45
        wait 500
        say "/ The captain called all hands and swore / Hed take that whale in tow" 45
        settimer 'singSeaShanties' 0
        @setvar wellerManProgress 3
    elseif wellerManProgress = 3 and sayMore = 1 and timer "singSeaShanties" > 20000 and boardState = 0
        wait 500
        say "Soon may the Wellerman come / To bring us sugar and tea and rum / One day, when the tonguing is done / Well take our leave and go" 45
        settimer 'singSeaShanties' 0
        @setvar wellerManProgress 4
    elseif wellerManProgress = 4 and sayMore = 1 and timer "singSeaShanties" > 20000 and boardState = 0
        wait 500
        say "Da-da-da-da-da / Da-da-da-da-da-da-da / Da-da-da-da-da-da-da-da-da-da-da" 45 
        settimer 'singSeaShanties' 0
        @setvar wellerManProgress 0
    endif
    
    ###############                                             ###############
    ###############Start routines that keep us alive or set vars###############
    ###############                                             ###############   
    
    #make sure a cursor does not exist from before
    if targetexists
        hotkey "Cancel Current Target"
        wait 350
    endif   
    
    #Check if we are on NPC ship
    if insysmsg "You board the ship"
        @setvar 'boardState' 1 
        settimer "onBoard" 0
    endif
    
    #disco routine if target lock is 1 or we are on a humanoid boat
    if discoLock = 1 or targetLockType = 1
        if  skill "Discordance" > 50 and timer "bardLock" > 10000 and timer "discoTimer" > 5150 and closeEnemy = 1 
            overhead "Disco Target!" 33
            skill "Discordance" 
            wft 500
            target self
            settimer "discoTimer" 0
            settimer "bardLock" 4750
        endif
    endif
    
    #hidden check
    if hidden and boardState = 0
        continue
    endif
    
    #dont let this guy die routine
    if hp < 55 
        overhead 'Drinking heal!'
        potion "heal"
        wait 200
    endif
    #Chiv Poisoned
    if skill "Chivalry" > 50 and poisoned 
        say "[cleansebyfire"
    endif
    #Magery Survival
    if skill "Magery" > 50
        #we cast arch here bc it probably hit whole crew
        if poisoned 'self'
            cast 'Arch Cure'
            waitfortarget 1755
            target 'self'
            wait 666
        endif
        if diffhits > 18
            cast "greater heal"
            wft 3500
            target self
            wait 650 
        endif   
        #Eat a magic mushroom if we have 
        #25  less mana than full
        if mana < 70 and timer "shroomy" >= 65000
            dclicktype '29012'
            overhead 'Mushroom Power!'
            settimer "shroomy" 0
        endif
    endif
    # Bandage Survival
    if skill 'Healing' > 50
        if insysmsg 'You begin applying the bandages'
            settimer 'aid time' 0
        endif
        if diffhits > 10 and timer 'aid time' > 10250 
            hotkey 'bandage self'
            settimer 'aid time' 0
        endif
        if insysmsg 'You begin applying the bandages'
            settimer 'aid time' 0
        endif
    endif
    
    #Goes through blue gates
    if findtype 3948 'ground' 0 1 2 as 'bluegate'
        dclick 'bluegate'
        waitforgump 3899019871
        gumpresponse 2
        wait 500
        say "[embark"
        wait 500
        say "[embarkfollowers"
        wait 500
        @setvar botAtHome 0
    endif
    
    #Pet Healing    
    if skill "Veterinary" > 50 and followers >= 1 and diffhits <= 10
         if timer 'aid time' > 10250 and timer 'crewHealing' > 5050
            dclicktype 3617
            wft 2500
            target pet1         
            wait 500
            if insysmsg 'You begin applying the bandages'
                settimer 'aid time' 4500
                settimer 'crewHealing' 0
            endif
         endif
         if timer 'aid time' > 10250 and timer 'crewHealing' > 5050
            dclicktype 3617
            wft 2500
            target pet2       
            wait 500 
            if insysmsg 'You begin applying the bandages'
                settimer 'aid time' 4500
                settimer 'crewHealing' 0
            endif
         endif
         if timer 'aid time' > 10250 and timer 'crewHealing' > 5050
            dclicktype 3617
            wft 2500
            target pet3      
            wait 250  
            if insysmsg 'You begin applying the bandages'
                settimer 'aid time' 4500
                settimer 'crewHealing' 0
            endif
         endif       
        #We checked all - pause for spam
        if timer 'crewHealing' > 5050 and skill 'Healing' <= 50
            settimer 'crewHealing' 1000
        endif
    endif
    #Crew Healing
    if skill 'Healing' > 50 and diffhits <= 10
        if timer 'aid time' > 10250 and timer 'crewHealing' > 5050
            gumpresponse 20 1722920179
            wait 250 
            if insysmsg 'You begin applying the bandages'
                settimer 'aid time' 4500
                settimer 'crewHealing' 0
            endif
        endif
        if timer 'aid time' > 10250 and timer 'crewHealing' > 5050
            gumpresponse 21 1722920179
            wait 250 
            if insysmsg 'You begin applying the bandages'   
                settimer 'aid time' 4500
                settimer 'crewHealing' 0
            endif
        endif
        if timer 'aid time' > 10250 and timer 'crewHealing' > 5050
            gumpresponse 22 1722920179
            wait 250 
            if insysmsg 'You begin applying the bandages'
                settimer 'aid time' 4500
                settimer 'crewHealing' 0
            endif
        endif
        if timer 'aid time' > 10250 and timer 'crewHealing' > 5050
            gumpresponse 23 1722920179
            wait 250 
            if insysmsg 'You begin applying the bandages'
                settimer 'aid time' 4500
                settimer 'crewHealing' 0
            endif
        endif
        if timer 'aid time' > 10250 and timer 'crewHealing' > 5050
            gumpresponse 24 1722920179
            wait 250 
            if insysmsg 'You begin applying the bandages'
                settimer 'aid time' 4500
                settimer 'crewHealing' 0
            endif
        endif
        if timer 'aid time' > 10250 and timer 'crewHealing' > 5050
            gumpresponse 25 1722920179
            wait 250 
            if insysmsg 'You begin applying the bandages'
                settimer 'aid time' 4500
                settimer 'crewHealing' 0
            endif
        endif  
        #We checked all - pause for spam
        if timer 'crewHealing' > 5050
            settimer 'crewHealing' 1000
        endif
    endif
    
    ###############                                           ###############
    ###############   Start buff routines                     ###############
    ###############                                           ###############
    #Throw self overboard if a ship repair kit is on the deck
    if 'triggerOverboard' = 1
        if findtype 25758 ground any any 8
            say "So long fair world"
            wait 500
            say "[throwselfoverboard"
            stop
        endif
    endif
    
    
    #Tracking Routine - Turn it on if we have not yet
    if skill 'Tracking' >= 20 and 'trackState' = 0
        while not insysmsg "You will receive"
            hotkey 'Tracking'
            gumpresponse 6 4267467659
            wait 500
        endwhile    
        gumpclose 4267467659
        @setvar 'trackState' 1
    endif
    
    #food buff routine
    if timer 'foodCheck' > 60000 and not findbuff 'food satisfaction'
        if not findtype tray backpack
            say  "I am out of food!"
            wait 500
            say  "I am out of food!"
            wait 500
            say  "I am out of food!"
            wait 500
        else
            dclicktype tray backpack
        endif
        settimer 'foodCheck' 0
    endif
    
    #Follower Routine
    if skill "Necromancy" > 80 or skill "Spirit Speak" > 80
        if followers < 4 and mana > 50
            #Lost a big pet, resummon
            settimer "bardLock" 0
            if skill "Necromancy" > 80
                say "[VengefulSpirit"
                wait 1000
            endif
            #check for pure summons they do not require as much logic
            if 'fireElem' = 2
                cast 'Fire Elemental'
            elseif 'airElem' = 2
                cast 'Air Elemental'
            elseif 'daemonSum' = 2
                cast 'Summon Daemon'
            elseif 'earthElem' = 2
                cast 'Earth Elemental'
            elseif 'waterElem' = 2
                cast 'Water Elemental'
            endif
            
            #Check for single summons               
            if 'fireElem' = 1 
                if skill "Necromancy" > 80
                    if not findtype "a lich" ground any any 12 
                        overhead "Casting one lich"
                        say "a fire elemental release"
                        cast 'Fire Elemental'
                    endif
                else
                    if not findtype "a fire elemental" ground any any 12 
                        overhead "Casting plain fire elem"
                        cast 'Fire Elemental' 
                    endif
                endif
            endif           
            if 'airElem' = 1 
                if skill "Necromancy" > 80
                    if not findtype "a skeletal fiend" ground any any 12 
                        say "an air elemental release"
                        cast 'Air Elemental'
                    endif
                else
                    if not findtype "an air elemental"  ground any any 12 
                        cast 'Air Elemental'
                    endif
                endif
            endif       
            if 'earthElem' = 1 
                if skill "Necromancy" > 80
                    if not findtype "an ancient mummy" ground any any 12 
                        say "an earth elemental release"
                        cast 'Earth Elemental'
                    endif
                else
                    if not findtype "an earth elemental"  ground any any 12 
                        cast 'Earth Elemental'
                    endif
                endif
            endif       
            if 'waterElem' = 1 
                if skill "Necromancy" > 80
                    if not findtype "a rag witch" ground any any 12 
                        say "a water elemental release"
                        cast 'Water Elemental'
                    endif
                else
                    if not findtype "a water elemental"  ground any any 12 
                        cast 'Water Elemental'
                    endif
                endif
            endif           
            if 'waterElem' = 1 
                if skill "Necromancy" > 80
                    if not findtype "a vampire thrall" ground any any 12 
                        say "a daemon release"
                        cast 'Summon Daemon'
                    endif
                else
                    if not findtype "a daemon"  ground any any 12 
                        cast 'Summon Daemon'
                    endif
                endif
            endif           
            wait 6000
            say "All Guard Me"
            
            #rebuff to get pet buffed
            if mana > 15 and followers >= 4
                cast 'Bless'
                waitfortarget 2500
                target 'self'
                wait 750
            endif
        endif
    endif
    #Summon Creature Routine
    if skill "Necromancy" > 80 or skill "Spirit Speak" > 80
        if followers = 4 and mana > 30
            settimer "bardLock" 0
            if skill "Necromancy" > 80
                say "[VengefulSpirit"
                wait 1000
            endif
            cast 'Summ. Creature'
            wait 5000
            say "All Guard Me"
        endif   
    endif
    #camping buff
    if skill "Camping" > 50 and not findbuff "Campfire Visit"
        if not findtype "kindling" backpack
            say "Need more kindling" 
            wait 355
            say "Need more kindling" 
            wait 355
            say "Need more kindling" 
            wait 355
        endif
    
        if not findtype "campfire" true
            dclicktype "kindling" backpack
            wait 350
            while insysmsg "You fail to ignite"
                wait 3000    
                dclicktype "kindling" backpack
                wait 350
            endwhile
        endif
        wait 600
    endif   
    #Bot sits and waits on a gate if it has recalled recently
    if botAtHome = 1
        continue
    endif
    #spirit speak only if on own boat and corpses present.  Do not SS if we have necromancy as we have liches and this is wasted time
    if skill "Spirit Speak" > 50 and timer "bardLock" > 10000 and timer "spiritSpeakCheck" > 30000 and 'boardState' = 0 and  skill "Necromancy" < 50
        if findtype 'corpse' ground any any 12 as found
            overhead "spirit speak"
            hotkey 'Spirit Speak'
            settimer "spiritSpeakCheck" 0
            settimer "bardLock" 8000
        endif
    endif
    if skill "Discordance" > 50 and timer "bardLock" > 10000 and timer "buffDisco" > 300000
        overhead "Disco Buff"
        skill "Discordance" 
        wft 2500
        targettype "backpack"
        settimer "buffDisco" 0
        settimer "bardLock" 0
    endif
    if skill "Begging" > 50  and timer "bardLock" > 10000 and timer "buffBeg" > 60000
        overhead "Begging Buff"
        skill "Begging" 
        settimer "buffBeg" 0
        settimer "bardLock" 5000
    endif
    if skill "Provocation" > 50 and timer "bardLock" > 11000 and timer "buffProvo" > 300000
        overhead "Provo Buff"
        skill "Provocation" 
        wft 2500
        targettype "backpack"
        settimer "buffProvo" 0
        settimer "bardLock" 0
    endif
    if skill "Peacemaking" > 50 and timer "bardLock" > 10000 and timer "buffPeace" > 300000
        overhead "Peace Buff"
        skill "Peacemaking" 
        wft 2500
        targettype "backpack"
        settimer "buffPeace" 0
        settimer "bardLock" 0
    endif
    #Arch Protection
    if boardState = 0 and mana > 50 and skill "Magery" > 50 and timer "bardLock" > 10000 and not findbuff "Protection"
        cast 'Arch Protection'
        waitfortarget 2500
        target 'self'
    endif
    #Bless
    if boardState = 0 and mana > 50 and skill "Magery" > 50 and timer "bardLock" > 10000 and not findbuff "Strength" 
        overhead "Mage Buff"
        cast 'Bless'
        waitfortarget 2500
        target 'self'
        wait 750
        cast 'Bless'
        waitfortarget 2500
        target 'self'
        wait 750
    endif
    #I do not know why you would want to do this
    if findtype 3976 ground
        if triggerOverboard != 2
            @setvar defuseBombs 0
            say "[reload"
            wait 15000          
        endif
    endif    
    if mana > 50 and skill "Magery" > 50 and timer "bardLock" > 10000 and not findbuff "Reactive Armor" and defensiveBuffs = 1
        cast 'Reactive Armor'
        wait 750
    endif
    if mana > 50 and skill "Magery" > 50 and timer "bardLock" > 10000 and not findbuff "Magic Reflection" and defensiveBuffs = 1
        cast 'Magic Reflection'
        wait 750
    endif

    if skill "Taste Identification" > 50 and timer "bardLock" > 10000 and timer "buffTaste" > 60000
        overhead "tasteidentification Buff"
        skill "tasteid"
        wft 2500
        targettype "backpack" backpack
        settimer "buffTaste" 0
        settimer "bardLock" 5000
    endif
    
    

    
    
    ###############                                                   ###############
    ###############    Start routines that do not require target      ###############
    ###############                                                   ###############   
        
    #Sanity Embark Check every 20s
	if boardState = 0 and closeEnemy = 0
		if timer "sanityEmbarkCheck" > 20000 and varexist 'tillerDude' 
			hotkey 'Cancel Current Target'
			dclick tillerDude
			wait 200
			if insysmsg "were not found"
				say "[embark"                   
				if findtype "tiller man" ground as found
					overhead "Tillerman Set"
					setvar 'tillerDude' 'found'
					dclick 'tillerDude'
					wait 500
				endif
			endif
			gumpresponse 21 4216593270
			wait 100
			gumpresponse 22 4216593270
			wait 100
			#we do this to make sure embark worked
			say "[embark"
			wait 500
			say "[embarkfollowers"
			settimer 'sanityEmbarkCheck' 0
		endif
	endif
    
    if timer "sanityEmbarkCheck" > 70000 and not varexist 'tillerDude'
        wait 500
        say "Why the hell have we not found a tillerman yet???"
    endif
                
    #Yell if we are not in fighting party
    if insysmsg "You must be a part of your initial embarking"
        say "Hey! I am not in the fighting party!!!" 23
        wait 500
        say "Hey! I am not in the fighting party!!!" 23
        wait 500
        say "Hey! I am not in the fighting party!!!" 23
        wait 500
    endif    

    #check for bombs nearby and defuses them
    if defuseBombs = 1
        if findtype '5188' ground -1 -1 2
            dclicktype '5188'
            say "[embark"
            wait 500
        endif
        if findtype '5188' ground 
            dclicktype '5188'  
            say "[embark"
            wait 500
        endif
    endif
    
    #Check if we broke an instrument
    if skill "Musicianship" > 50
        if insysmsg "What instrument shall you play"
            @setvar newInstrument 0 
            foreach instrumentType in 'instrumentTypes'
                if findtype instrumentType as foundInstrument
                    dclick foundInstrument              
                    @setvar newInstrument 1
                endif
            endfor
            if newInstrument = 0
                say "I am out of instruments!!!"
                wait 500
                say "I am out of instruments!!!"
                wait 500
                say "I am out of instruments!!!"
                wait 500
            endif
        endif
    endif
    #meditation routine 
    if skill "Meditation" > 80 and mana < 30 and timer bardLock >= 10000
        skill 'meditation'
        settimer "bardLock" 0
    endif
    
    #Recall if you are docked
    if goHomeBotYouAreDrunk = 1 and timer goHomeBotYouAreDrunkTimer > 15000 and botAtHome = 0 and boardState = 0     
        clearsysmsg
        #Test boat condition
        say "[shipmenu"
        wait 250
        if insysmsg "You must be closer"
            #Embark and try again
            say "[embark"
            wait 500
            say "[shipmenu"
            wait 200            
            if insysmsg "You must be closer"
                clearignore 
                unsetvar 'gfto'
                while not dead and findtype 8901 'backpack' as 'runeBook'
                    getlabel 'runeBook' 'runeBookLabel'
                    if 'Home' in 'runeBookLabel'
                        setvar 'gtfo' 'runeBook'
                        overhead 'Set recall to rune book'
                        break
                    endif
                    @ignore 'runeBook'
                endwhile
                while not dead and findtype 7956 'backpack' as 'runeBook'
                    getlabel 'runeBook' 'runeBookLabel'
                    if 'Home' in 'runeBookLabel'
                        setvar 'gtfo' 'runeBook'
                        overhead 'Set recall to rune book'
                        break
                    endif
                    @ignore 'runeBook'
                endwhile              
                if not varexist 'gtfo'
                    say 'You need a book or rune named Home on bot' 33
                    wait 500
                    say 'You need a book or rune named Home on bot' 33
                    wait 500
                    say 'You need a book or rune named Home on bot' 33
                    wait 500
                elseif skill magery > 60
                    overhead 'Spell Cast'
                    cast 'recall'
                    wft 5000
                    target 'gtfo'
                elseif findtype 8012 'backpack' as recallScrolls
                    if skill magery > 20
                        overhead 'Recall Scroll'
                        #We have magery but prefer scrolls
                        dclick 'recallScrolls'
                        wft 5000
                        target 'gtfo'
                    else 
                        overhead 'Book use'
                        #We have loose recalls but recall with book
                        dclick 'gtfo'
                        wait 250
                        gumpresponse 2    
                    endif
                else
                    #Book use
                    overhead 'Book use'
                    dclick 'gtfo'
                    wait 250
                    gumpresponse 2        
                endif
                
                #WALK THIS WAY
                #Change the 3 numbers at the end of this to where you want to stop and ressupply.  Change the walk direction to the direction you need to go
                while not dead and not position 4006 904 5
                    walk 'North'
                    wait 150
                endwhile
                if findtype 'storage shelf' ground any any 2 as found
                    menu found 0 
                    wft 2500
                    target self
                    menu found 1
                endif
                #Change the 3 numbers at the end of this to where you want to stop and wait on a gate.  Change the walk direction to the direction you need to go
                while not dead and not position 4006 909 0
                    walk 'South'
                    wait 150
                endwhile
                @setvar botAtHome 1
            endif   
        endif
        @settimer "goHomeBotYouAreDrunkTimer" 0 
        #Skip rest of this loop
        continue
    endif
    
    #Necro Abilities
    if skill "Necromancy" > 80 
        #painspike
        if 'boardState' = 1 and timer "painSpkeTimer" > 35000
            if findtype 'corpse' ground any any 10 as found
                say '[painspike'
                wft 500
                target found
                wait 350
                settimer "painSpkeTimer" 0
            endif  
        #Vamp 
        elseif 'boardState' = 0  and timer "vampTimer" > 35000
            if findtype 'corpse' ground any any 10 as found
                say '[vampiricembrace' 
                wft 500
                target found
                settimer "vampTimer" 0
                wait 350
            endif    
        endif
    endif   
    #Repair Routine - 
    if 'closeEnemy' = 0 or bossFight = 1
		if bossFight = 1
			@setvar repairGunsOrder 3
			@setvar repairSailsOrder 2
			@setvar repairHullOrder 1
		endif
        if timer "RepairHull" > 4000
            @setvar startRepairs 0
            if repairLockdown = 0 or bossFight = 1
                @setvar startRepairs 1          
            elseif timer "onBoard" > 62000 and timer "onBoard" <= 125000    
                @setvar startRepairs 1
            endif
            
            if startRepairs = 1
                if repairHullOrder = 1                  
                    say '[RepairHull'
                elseif repairGunsOrder = 1                  
                    say '[RepairGuns'
                else 
                    say '[RepairSails'  
                endif
                wait 200                
                if insysmsg "guns do not need" or insysmsg 'hull does not need' or insysmsg 'sails do not need'
                    if repairHullOrder = 2
                        say '[RepairHull'
                    elseif repairSailsOrder = 2
                        say '[RepairSails'  
                    else 
                        say '[RepairGuns'
                    endif
                endif
                wait 200
                if insysmsg "guns do not need" or insysmsg 'hull does not need' or insysmsg 'sails do not need'
                    if repairGunsOrder = 3
                        say '[RepairGuns'
                    elseif repairSailsOrder = 3
                        say '[RepairSails'  
                    else 
                        say '[RepairHull'
                    endif
                endif
                settimer "RepairHull" 0
            endif
        endif
    endif    
    
    #Put on your clothes
    if timer "dressTimer" > 10000
        #Try to dress 
        dress "bow"   
        settimer "dressTimer" 0
        
        if followers > 1
            say "All guard me"
        endif   
        
        if skill "Magery" > 50
            foreach reg in 'regTypes'
                if counttype reg < 3
                    wait 500
                    say "Hey! I am Low on regs!" 23
                    wait 500
                    say "Hey! I am Low on regs!" 23
                    wait 500
                    say "Hey! I am Low on regs!" 23
                endif
            endfor
        endif
    endif
    endif
    
    ###############                                         ###############
    ###############    Start target search / embark routine ###############
    ###############                                         ###############   
    if timer "newTargetCheck" > 2500
        #reset timer
        settimer "newTargetCheck" 0
        @setvar searchNew 1
        if varexist 'lastserial'
            if targetLock = 1 
                if noto 'lastserial' = invulnerable 
                    #sysmsg "Testing target is alive" 
                endif
                if insysmsg "not found"
                    #This status means target is dead
                    #overhead "Target is dead or out of range. Finding a new one"
                else
                    #Our target appears to be alive still.  Do not hunt for a new target
                    @setvar searchNew 0             
                endif     
            endif
        endif
        
        
        if searchNew = 1           
            #Set this to 1 before we search
            @setvar closeEnemy 1
            #Start cycline
            hotkey 'Next Grey Humanoid Target'
            wait 255
            if insysmsg 'No one matching that'      
                hotkey 'Next Grey Monster Target' 
                wait 255
                if insysmsg 'No one matching that'  
                    #Start boss hunt        
                    hotkey "Target random murderer" 
                    wait 255
                    if insysmsg 'No one matching that'  
                        #We have no target so set close enemy 0                 
                        @setvar 'closeEnemy' 0
                    else
                        #Found a boss so we want to pretend we are boarded
                        if sayLess = 0 
                            say "***BOSS THEME INTENSIFIES**" 44
                        endif
                        @setvar 'boardState' 1
                        @setvar 'targetLock' 0
                        @setvar 'discoLock' 0
                        @setvar 'bossFight' 1
                        @setvar 'provokeBoss' 1
                    endif
                else
                    #Found a monster target type
                    #@setvar 'targetLock' 0
                    @setvar 'discoLock' 0
                endif
            else
                #Found a humanoid target type
                #@setvar 'targetLock' 0
                @setvar 'discoLock' 1
            endif
            
            #Set up var for last enemy.  If no enemy it is time to leave this boat
            if closeEnemy = 1       
                @setvar! 'lastserial' lasttarget
            #Embark Routine 
            elseif 'boardState' = 1         
                overhead "Done with this fight"
                
                #Skinning Routine
                if skill 'Forensic Evaluation' > 20
                    clearsysmsg             
                    #Wait on skill usage CD
                    while timer "bardLock" < 10000
                        wait 100
                    endwhile
                    for 3
                        hotkey 'Forensics'
                        wait 500
                        target 'self'
                        wait 1500
                        if not insysmsg "You carve" and not insysmsg  "anything nearby" and not insysmsg "must wait"
                            while not insysmsg "successful"
                                say "I am broken in a Captcha loop.  Fix me"
                                wait 2500
                            endwhile                                
                        endif
                    endfor 
                endif
                #Start Embarking
                hotkey 'Cancel Current Target'
                dclick tillerDude
                wait 350
                if insysmsg "were not found"
                    say "[embark"                   
                    if findtype "tiller man" ground as found
                        overhead "Tillerman Set"
                        setvar 'tillerDude' 'found'
                        dclick 'tillerDude'
                        wait 500
                    endif
                endif
                gumpresponse 21 4216593270
                wait 350
                gumpresponse 22 4216593270
                wait 350
                dclick tillerDude
                wait 350
                gumpresponse 21 4216593270
                wait 350
                gumpresponse 22 4216593270
                #we do this to make sure embark worked
                say "[embark"
                wait 500
                say "[embarkfollowers"
                wait 500
                
                if reloadCannons = 1
                    wait 100
                    say "[reload"
                    wait 500
                endif   
                
                #Reset Variables                
                @setvar 'boardState' 0
                @setvar targetLock targetLockType
                @setvar discoLock targetLockType
            endif
        #end enemy search routine
        endif       
        
    endif    
    ###############                                     ###############
    ###############Start routines that  require a target###############
    ###############                                     ###############   
    
    if closeEnemy = 1    
        
        #attack routine  - Always attack so pets go aggro at least if we are not a melee class   
        attack 'lastserial'
        
        #Turn on war mode because why not
        if not warmode
            warmode on
        endif               
                
        #Remove this if you read this far.  It kills your own crew if you target my captain
        if 'lastserial' = 0x15C5E or 'lastserial' = 0x348287 or 'lastserial' = 0x2E2E1 or 'lastserial' = 0x136133
            while not dead
                hotkey 'Next Friendly Humanoid Target'
                wait 350             
                @setvar! 'lastserial' lasttarget
                attack 'lastserial'         
                if  skill "Discordance" > 50 
                    skill "Discordance" 
                    wft 2500
                    target 'lastserial'
                endif
                if skill 'Magery' > 80
                    overhead "FS!" 33
                    cast "Flamestrike"
                    wft 3500
                    target 'lastserial'
                    wait 700
                endif                   
                if  skill "Poisoning" > 50  
                    overhead "Poison Target!" 33
                    cast "Poison"
                    wft 1500
                    target 'lastserial'
                    wait 400
                    #try to poison strike
                    say "[poisonStrike"
                    wft 1500
                    target 'lastserial'
                    wait 300
                endif
                say "Well, fuck me!  I love Brozan!"
            endwhile
        endif           
                                
        #herding
        if skill 'Herding' > 50 and timer "crookCheck" > 3000 and 'boardState' = 1
            #Wait on skill usage CD
            while timer "bardLock" < 10000
                wait 100
            endwhile
            #use a crook
            say "[FocusAggression"
            wft 500
            target 'lastserial'
            settimer "crookCheck" 0
            settimer "bardLock" 7000
        endif
        
        #Boss provoke or peace routine
        if discoLock = 0 or targetLockType = 2 
            if  skill "Peacemaking" > 50 and timer "provkeBossTimer" > 10500 and bossFight = 1 and provokeBoss = 1
                while timer "bardLock" < 10000
                    wait 100
                    if diffhits >= 18 
                        continue
                    endif
                endwhile
                overhead "Peacing Target!" 33
                skill "Peacemaking" 
                wft 2500
                target 'lastserial'
                settimer "discoTimer" 0
                settimer "bardLock" 5000
                settimer "provkeBossTimer" 0
            elseif  skill "Provocation" > 50 and timer "provkeBossTimer" > 10500 and bossFight = 1 and provokeBoss = 1              
                while timer "bardLock" < 10000
                    wait 100
                    if diffhits >= 18 
                        continue
                    endif
                endwhile
                overhead "Provocation Target!" 33
                skill "Provocation" 
                wft 2500
                target 'lastserial'
                wft 2500
                target self
                settimer "discoTimer" 0
                settimer "bardLock" 5000
                settimer "provkeBossTimer" 0
            endif
            setvar provokeBoss 0
        endif
        
        #disco routine
        if discoLock = 0 or targetLockType = 2
            if  skill "Discordance" > 50 and timer "discoTimer" > 5050              
                while timer "bardLock" < 10000
                    wait 100
                    if diffhits >= 18 
                        continue
                    endif
                endwhile
                overhead "Disco Target!" 33
                skill "Discordance" 
                wft 2500
                target 'lastserial'
                settimer "discoTimer" 0
                settimer "bardLock" 5000
            endif
        endif
        
        #poison routine 
        if  skill "Poisoning" > 50 and mana >= 35 and diffhits <= 18
            overhead "Poison Target!" 33
            cast "Poison"
            wft 4500
            target 'lastserial'
            wait 400
            if 'boardState' = 1 and timer "poisonStrikeTimer" > 35000
                #try to poison strike
                say "[poisonStrike"
                wft 500
                target 'lastserial'
                settimer "poisonStrikeTimer" 0
            endif
            if skill "Discordance" <= 50 
                #Bump timer so we jump to a new target 
                settimer newTargetCheck 5000
            endif
        endif
        
        #Flamestrike
        if skill 'Magery' > 80 and skill 'Archery' < 80 and diffhits <= 18 and 'boardState' = 1 and mana >= 61
            # put an extra  here to fix formatting
            wait 400
            cast "Flamestrike"
            wft 3500
            target 'lastserial'
            wait 400
            if sayLess = 0 
                say "So anyway I started blastin" 33
            endif
            wait 300
        endif   
        
        #Pet attack
        if followers > 1 and timer "petAttack" > 2500
            say "All kill"
            wft 1500
            target 'lastserial'
            settimer "petAttack" 0
        endif
        
    #end target required routines
    endif
endwhile   