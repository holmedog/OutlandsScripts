# Clear sys messaages
clearsysmsg

# Setup some timers
if not timerexists 'aid time'
    createtimer 'aid time'
    settimer 'aid time' 20000
endif    
createtimer 'healpot time'
createtimer 'anti pot spam'
createtimer 'magery heal'
createtimer 'low mana'
createtimer 'stayalive spam'
createtimer 'hidy'
createtimer 'stealthy'

hotkey  'Hiding'


# While alive, try to stay alive.
while not dead
    wait 100

    # Display script running every 6 seconds. 
    if timer 'stayalive spam' > 6000
        overhead 'Stay Alive Running!' 66
        settimer 'stayalive spam' 0
    endif

    # If hidden, skip
    if hidden
        continue
    endif

    if insysmsg 'You begin to move'
        overhead 'We Moved!' 33
        settimer 'stealthy' 0
    endif
    if insysmsg 'You feel comfortable'
        overhead 'Move it!' 33
    endif
    if timer 'stealthy' > 10000 and timer 'stealthy' < 15000 and timer 'hidy' > 1000
        overhead 'Use a damn skill!' 66
        settimer 'hidy' 0
    endif

    # Tracking
    if insysmsg 'Now tracking'
        overhead 'Tracking Found!' 33
    endif

    # If paralyzed, attempt to pouch.
    if insysmsg 'you cannot move'
        overhead 'Paralyzed, attempting pouch escape' 88
        say [pouch
    endif

    # Healing Survival
    if skill 'Healing' > 1
        if insysmsg 'You begin applying the bandages'
            settimer 'aid time' 0
        endif
        if hits < 100 and timer 'aid time' > 10250 
            hotkey 'bandage self'
            settimer 'aid time' 0
        elseif maxhits >= 119 and hits < 119
            hotkey 'bandage self'
            settimer 'aid time' 0
        endif
    endif

   # Cure Survival
    if poisoned 'self' and timer 'anti pot spam' > 1000
        overhead 'Used: Cure Potion' 33
        hotkey 'Drink Cure' 
        settimer 'anti pot spam' 0
    endif

    # Heal Potion
    if hits < 55 and timer 'healpot time' > 10000 and timer 'anti pot spam' > 1000
        overhead 'Used: Heal Potion' 33
        hotkey 'Drink Heal'
        settimer 'healpot time' 0
        settimer 'anti pot spam' 0
    endif 

endwhile

if dead
    stop
endif