# Fishing with Captcha Detection (cooldown) by Jaseowns
# Modified for FS by Brozan


# Requirements
#    Setup a cooldown named "Fish" - it must match that name.
#    If you run the script without the cooldown, it will show you instructions.
#    but you want to have 3 entries with 10 seconds on each
#            sysmsg: fish
#            sysmsg: You retrieve a
#            overhead: You catch
#    The script works by checking the cooldown firing

clearsysmsg 

if not varexist dockMaster
    overhead "Target your DockMaster"
    @setvar! dockMaster
endif

@setvar! jaseFishingCD 9000
if not timerexists jaseFishingTimer
    createtimer jaseFishingTimer
    settimer jaseFishingTimer jaseFishingCD
endif

cooldown Fish jaseFishingCD

if not cooldown Fish
    overhead "Please setup a cooldown named:" 34
    overhead "Fish" 88
    sysmsg "Please setup a cooldown named:" 34
    sysmsg "Fish" 88
    sysmsg "Add these with 10 second cooldown" 66
    sysmsg "sysmsg: fish"
    sysmsg "sysmsg: You retrieve a"
    sysmsg "overhead: You catch"
    stop
endif

while not dead     
    if diffhits > 2
        clearsysmsg 
        #open DM gump
        menu dockMaster 1
        #Click Dock
        waitforgump 3287496917
        gumpresponse 7 3287496917
        wait 300
        guild "FISHER ALERT: I had to dock! PvP Here if you want it!"
        while insysmsg "The ship has not been"
            waitforgump 3287496917
            gumpresponse 7 3287496917
            wait 300
        endwhile
    endif

    if lhandempty
        if findtype "fishing pole" backpack as item
            lift item
            drop self lefthand
        else
            overhead "Out of poles!" 34
            stop
        endif
        wait 650
    endif
    
    if timer jaseFishingTimer >= jaseFishingCD
        if cooldown Fish
           say "[greaterability"
           hotkey 'Use item in hand'
           settimer jaseFishingTimer 0
           cooldown Fish 0
        else
            wait 1000
            if not cooldown Fish
                overhead 'Captcha break!' 34
                for 20
                    overhead 'Awaiting Captcha...' 34
                    wait 1000
                    if insysmsg "You do not see"
                        overhead "Move your damn boat"
                        replay
                    endif
                    if insysmsg "World save complete"
                        overhead "World Saved"
                        replay
                    endif
                    #check for getting shot
                    if diffhits > 2
                        #open DM gump
                        menu dockMaster 1
                        #Click Dock
                        waitforgump 3287496917
                        gumpresponse 7 3287496917
                        wait 300
                        guild "FISHER ALERT: I had to dock! PvP Here if you want it!"
                        while insysmsg "The ship has not been"
                            waitforgump 3287496917
                            gumpresponse 7 3287496917
                            wait 300
                        endwhile
                    endif
                    if insysmsg 'Captcha successful'
                        overhead 'Success - continue on!' 88
                        wait 500
                        replay
                    endif 
                endfor
                
                #Catpcha existed too long, exit
                #open DM gump
                menu dockMaster 1
                #Click Dock
                waitforgump 3287496917
                gumpresponse 7 3287496917
                wait 300
                while insysmsg "The ship has not been"
                    waitforgump 3287496917
                    gumpresponse 7 3287496917
                    wait 300
                endwhile
    
                stop
            endif
        endif
    endif
    
endwhile