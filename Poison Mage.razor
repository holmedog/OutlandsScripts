
##################
## If you are using this then you probably know how to contact Brozan on Discord if you have issues
############

#MAKE SURE your wizards satchel is in your backpack not on your char

#Search for "WALK THIS WAY" and edit it for how to walk into your house

# Set this to one, if you want to cast poison once no matter what
# If you have 100 poisoning and want Lethal, set this to 0
@setvar! castPoisonAnyway 0

# We will spam mind blast when other skills are on cooldown
# If you set this to 0 it will use flamestrike
@setvar! spamMindBlastOverFlamestrike 0

# This will auto replay this script if you want it too
@setvar! autoBotEnabled 1

# This will make sure you have bless on
@setvar! blessYourself 1

#Set this to 1 if you want to go to the next spot
@setvar! recallEnabled 1

#Set this to 1 if you are ok with Deadly poison
@setvar! deadlyOK 1

###############
###  Choose your spells
######
@setvar! castCurse 1
@setvar! castManadrain 1
@setvar! castLightning 0
@setvar! castFireball 0
@setvar! castHarm 0
@setvar! castMagicArrow 0

###############
### Edit these cooldowns to match your wizard grimoire
######
@setvar! canYouMakeAMushroom 1
@setvar! cdLightning 60000
@setvar! cdMagicArrow 20000
@setvar! cdHarm 20000
@setvar! cdFireball 40000

###############
### Necro Cooldowns
######
@setvar! cdEvilOmen 31000
@setvar! cdVampiricEmbrace 31000
@setvar! cdCorpseSkin 31000
@setvar! cdMindRot 31000

#### Buff Cooldowns
@setvar! cdMushroom 60000
@setvar! cdMagicShield 60000
@setvar! cdReactiveArmor 60000
@setvar! cdMeditation 10000
@setvar! cdMeditationMini 3000
@setvar! cdKillTarget 60000

#### Pet Names, unique to you
if not listexists petNameList
    createlist petNameList
endif
if list petNameList = 0
    pushlist petNameList "YoGabbaGabba"
    pushlist petNameList "YoGabbaGabbaB"
    pushlist petNameList "YoGabbaGabbaC"
    pushlist petNameList "YoGabbaGabbaD"
    pushlist petNameList "YoGabbaGabbaE"
    pushlist petNameList "YoGabbaGabbaF"
endif

##################################################################
# 
#   Hopefully you do not have to edit after this :)
#
##################################################################
clearsysmsg
#Prime this variable
if not varexist notSeen
    @setvar! notSeen 0
endif
#Prime this variable
if not varexist discordOn
    @setvar! discordOn 0
endif

@setvar! thisIsTrash 0

if not listexists 'firstRunSetup'
    createlist 'firstRunSetup'
    overhead "Select your runebook (relog to reset)"
    setvar! runeBookFarm

    getlabel runeBookFarm desc

    if runebook in desc
        overhead "Runebok in desc using those runes"
        @setvar! usingRuneBook 1 
    else
        overhead "Using a Rune Tome"
        @setvar! usingRuneBook 0
    endif
            
endif

#Create list of reagents
if not listexists 'regTypes'    
    createlist regTypes
    @pushlist 'regTypes' 'Blood Moss'
    @pushlist 'regTypes' 'Black Pearl%s%'
    @pushlist 'regTypes' 'Ginseng'
    @pushlist 'regTypes' 'Garlic'
    @pushlist 'regTypes' 'Sulfurous Ash'
    @pushlist 'regTypes' "Spider's Silk" 
    @pushlist 'regTypes' 'Nightshade' 
    @pushlist 'regTypes' 'Mandrake Root%s%' 
endif
# ' for formatting reasons

#Populate our runes
if not listexists 'runes'
    createlist 'runes'
endif
if list runes <= 0
    if usingRuneBook = 1
        #gumps here for recall spell use
        pushlist 'runes' 11
        pushlist 'runes' 17
        pushlist 'runes' 23
        pushlist 'runes' 29
        pushlist 'runes' 35
        pushlist 'runes' 41
        pushlist 'runes' 47
        pushlist 'runes' 53
        pushlist 'runes' 59
        pushlist 'runes' 65
        pushlist 'runes' 71
        pushlist 'runes' 77
        pushlist 'runes' 83
        pushlist 'runes' 89
        pushlist 'runes' 95
    else
        pushlist 'runes' 201
        pushlist 'runes' 202
        pushlist 'runes' 203
        pushlist 'runes' 204
        pushlist 'runes' 205
        pushlist 'runes' 206
        pushlist 'runes' 207
        pushlist 'runes' 208
        pushlist 'runes' 209
        pushlist 'runes' 210
        pushlist 'runes' 211
        pushlist 'runes' 212
        pushlist 'runes' 213
        pushlist 'runes' 214
        pushlist 'runes' 215
        pushlist 'runes' 216
        pushlist 'runes' 217
        pushlist 'runes' 218
        pushlist 'runes' 219
        pushlist 'runes' 220
        pushlist 'runes' 221
        pushlist 'runes' 222
        pushlist 'runes' 223
        pushlist 'runes' 224
        pushlist 'runes' 225
        @setvar tomeSideSwitch 0
    endif
endif   

if not gumpexists 622436516 and skill "Necromancy" >= 50
    say '[NecromancyHotbar'
    wait 500
endif

// Make sure we have a spell book attached
if findlayer self righthand as item
    // do nothing
elseif findtype "3834" backpack as item
    dclick item
else 
    overhead "No book bro!" 34
endif

if not timerexists reactiveArmorTimer
    createtimer reactiveArmorTimer
    settimer reactiveArmorTimer cdMagicShield
endif

if not timerexists recallSpam
    createtimer recallSpam
    settimer recallSpam 60000
endif

if not timerexists atThisSpot
    createtimer atThisSpot
    settimer atThisSpot 7000
endif

if not timerexists poisonStrikeTimer
    createtimer poisonStrikeTimer
    settimer poisonStrikeTimer 7000
endif

if not timerexists magicShieldTimer
    createtimer magicShieldTimer
    settimer magicShieldTimer cdMagicShield
endif

if not timerexists overheadSpam
    createtimer overheadSpam
    settimer overheadSpam 5000
endif

if not timerexists discoTimer
    createtimer discoTimer
    settimer discoTimer 60000
endif

if not timerexists magicMushroomTimer
    createtimer magicMushroomTimer
    settimer magicMushroomTimer cdMushroom
endif

if not timerexists magicArrowTimer
    createtimer magicArrowTimer
    settimer magicArrowTimer cdMagicArrow
endif

if not timerexists lightningTimer
    createtimer lightningTimer
    settimer lightningTimer cdLightning
endif

if not timerexists harmTimer
    createtimer harmTimer
    settimer harmTimer cdHarm
endif

if not timerexists fireballTimer
    createtimer fireballTimer
    settimer fireballTimer cdFireball
endif

if not timerexists evilOmenTimer
    createtimer evilOmenTimer
    settimer evilOmenTimer cdEvilOmen
endif

if not timerexists vampiricEmbraceTimer
    createtimer vampiricEmbraceTimer
    settimer vampiricEmbraceTimer cdVampiricEmbrace
endif

if not timerexists corpseSkinTimer
    createtimer corpseSkinTimer
    settimer corpseSkinTimer cdCorpseSkin
endif

if not timerexists mindRotTimer
    createtimer mindRotTimer
    settimer mindRotTimer cdMindRot
endif

if not timerexists meditiationTimer
    createtimer meditiationTimer
    settimer meditiationTimer cdMeditation
endif

if not timerexists killTargetTimer
    createtimer killTargetTimer
    settimer killTargetTimer cdKillTarget
endif

if not varexist myEarthPet
    @setvar! myEarthPet 0
endif
if not varexist myWaterPet
    @setvar! myWaterPet 0
endif
if not varexist mySummonAnimalPet
    @setvar! mySummonAnimalPet 0
endif

if diffhits < 30
    if followers = 0
        @setvar! followCount 0
    elseif followers = 1
        @setvar! followCount 1
    elseif followers = 2
        @setvar! followCount 2
    elseif followers = 3
        @setvar! followCount 3
    elseif followers = 4
        @setvar! followCount 4
    elseif followers = 5
        @setvar! followCount 5
    endif
    if dead myEarthPet or not find myEarthPet ground -1 -1 12
        if followers < 4
            while mana < 65
                if diffhits >= 30
                    replay
                endif
                if timer 'overheadSpam' > 2000
                    overhead "waiting for mana" 88
                    settimer 'overheadSpam' 0
                endif
                if not findbuff "Actively Meditating"
                    useskill "meditation"
                endif
                wait 200
            endwhile 
            say "[VengefulSpirit"
            while followers = followCount
                cast "earth elemental"
                wait 500
                if diffhits >= 30
                    hotkey '> Interrupt'
                    replay
                    break
                endif
            endwhile
            // 158 - an ancient mummy
            if findtype 158 ground -1 -1 6 as myPet
                if noto myPet = "friend" and list petNameList > 0
                    overhead "Renaming..."
                    foreach petName in petNameList
                        rename myPet petName
                        break
                    endfor
                    poplist petNameList "front"
                endif
                @setvar! myEarthPet myPet
                say "all guard me"
                replay
            endif
        endif
    endif
endif

if diffhits < 30
    if followers = 0
        @setvar! followCount 0
    elseif followers = 1
        @setvar! followCount 1
    elseif followers = 2
        @setvar! followCount 2
    elseif followers = 3
        @setvar! followCount 3
    elseif followers = 4
        @setvar! followCount 4
    elseif followers = 5
        @setvar! followCount 5
    endif
    if dead myWaterPet or not find myWaterPet ground -1 -1 12
        if followers < 4
            while mana < 65
                if diffhits >= 30
                    replay
                endif
                if timer 'overheadSpam' > 2000
                    overhead "waiting for mana" 88
                    settimer 'overheadSpam' 0
                endif
                if not findbuff "Actively Meditating"
                    useskill "meditation"
                endif
                wait 200
            endwhile
            say "[VengefulSpirit"
            while followers = followCount
                cast "fire elemental"
                wait 500
                if diffhits >= 30
                    hotkey '> Interrupt'
                    break
                endif
            endwhile
            // 24 - a lich
            if findtype 24 ground -1 -1 6 as myPet
                if noto myPet = "friend" and list petNameList > 0
                    overhead "Renaming..."
                    foreach petName in petNameList
                        rename myPet petName
                        break
                    endfor
                    poplist petNameList "front"
                endif
                @setvar! myWaterPet myPet
                say "all guard me"
                replay
            endif
        endif
    endif
endif

if followers = 4 and diffhits < 30
    while mana < 55
        if timer 'overheadSpam' > 2000
            overhead "waiting for mana" 88
            settimer 'overheadSpam' 0
        endif
        if diffhits >= 30
            replay
        endif
        if not findbuff "Actively Meditating"
            useskill "meditation"
        endif
        wait 200
    endwhile
    say "[VengefulSpirit"  
    while followers = followCount
        cast 'Summ. Creature'
        wait 500
        if diffhits >= 30
            hotkey '> Interrupt'
            replay
        endif
    endwhile        
    say "all guard me"
endif

# Eat food if we got it
if not findbuff "food" and findtype "tray" backpack as jood
    dclick jood
    wait 200
endif

# Use Taste ID if possible
if skill "Taste Identification" >= 50 and not findbuff "herb"
    useskill 'tasteidentification'
    wft 500
    target backpack
endif

if diffhits >= 30
    while not targetexists
        if diffhits >= 60
            cast 'Heal'
        else
            cast 'Greater Heal'                
        endif
        wait 50
        if hp = maxhp
            hotkey '> Interrupt'
            break
        endif
    endwhile
    if targetexists 'beneficial'
        target self
    endif
endif

while mana < 55
    if diffhits >= 30
        replay
    endif
    if timer 'overheadSpam' > 2000
        overhead "waiting for mana" 88
        settimer 'overheadSpam' 0
    endif
    if not findbuff "Actively Meditating"
        useskill "meditation"
    endif
    wait 200
endwhile

setvar! goOnAndGet 0
if thisIsTrash = 1
    say "all kill"
    wait 200
    while targetexists 
        // wait
        wait 50
    endwhile
else
    hotkey 'Target Closest Non-Friendly Monster'
    wait 200
    if insysmsg "No one matching that was found"        
        setvar! goOnAndGet 1
    endif
    if notSeen = 1      
        setvar! goOnAndGet 1
        @setvar! notSeen 0
    endif   
    foreach reg in 'regTypes'
        if counttype reg < 7
            overhead "Hey! I am Low on regs!" 23
            @setvar goOnAndGet 1
        endif
    endfor 
    if not findtype 3852 self
        overhead 'Out of heal potions!' 34
        @setvar goOnAndGet 1
    endif
    if goOnAndGet = 1
        foreach rune in runes
            overhead "Brozan is the best! (Also I am recalling)" 34
            if recallEnabled = 1
                #reset variables
                @setvar! discordOn 0
                if followers < 5
                    overhead "Need summons before leaving"
                    replay
                endif
                #Skin corpses if we have been here 4s               
                if findtype 'corpse' ground any any 2 as found
                    #Check for a blade
                    if findtype '11552' backpack
                        hotkey 'Forensics'
                        wft 500
                        target 'self'
                        wait 255
                    else
                        #No blade, open corpses
                        while findtype 0x2006 'ground' 'any' 1 2 as 'found'
                            dclick 'found'
                            pause 400
                            @ignore 'found'
                        endwhile
                    endif               
                    @ignore 'found'                 
                    #pause for looting
                    wait 4000
                endif    
                if poplist runes front
                    #Recall to next rune
                    overhead "Heading to Rune: [{{rune}}]"
                endif
                @setvar recallOnce 1
                while insysmsg "Your concentration is disturbed" or insysmsg "book needs time" or recallOnce = 1                
                    @setvar recallOnce 0
                    say "All guard me"
                    dclick runeBookFarm
                    wait 200
                    clearsysmsg 
                    if diffweight <= 60 
                        overhead "Too fat.  Dump Routine"
                        #Cast recall til you are home
                        #Walk this way
                        #Change the 3 numbers at the end of this to where land at recall location
                        
                        if diffweight < 5
                            hotkey "Drink Strength"
                        endif
                        while not position 4007 909 0
                            cast "Recall"
                            while not targetexists and not insysmsg "Your concentration is disturbed" and not position 4007 909 0
                                wait 100
                            endwhile
                            target runeBookFarm
                            wait 1000
                        endwhile    
                        #Loot Routine
                        #WALK THIS WAY
                        #Change the 3 numbers at the end of this to where you want to stop and ressupply.  Change the walk direction to the direction you need to go
                        while not position 4007 904 5
                            walk 'North'
                            wait 150
                        endwhile
                        if findtype 'storage shelf' ground any any 2 as found
                            menu found 0 
                            wft 2500
                            target self
                            menu found 1                
                            gumpclose 3232825965        
                            if skill "Magery" > 50
                                while counttype 29012 backpack < 35 and not insysmsg "cheese"
                                    cast "create food"
                                    wait 1000
                                    if mana < 70 and insysmsg "use another magic"
                                        dclicktype 29012
                                    endif
                                    menu found 1
                                endwhile
                            endif
                            menu found 1
                        endif           

                        #If you need to open chests do so here
                        dclick 0x5B7C7BBD
                        wait 600

                        #"Starting Organizers"
                        organizer 1
                        while queued
                            wait 1000
                        endwhile
                        organizer 2
                        while queued
                            wait 1000
                        endwhile
                        organizer 3
                        while queued
                            wait 1000
                        endwhile
                        organizer 4
                        while queued
                            wait 1000
                        endwhile
                        organizer 5
                        while queued
                            wait 1000
                        endwhile
                        organizer 6
                        while queued
                            wait 1000
                        endwhile
                        organizer 7
                        while queued
                            wait 1000
                        endwhile
                        organizer 8
                        while queued
                            wait 1000
                        endwhile
                        organizer 9
                        while queued
                            wait 1000
                        endwhile
                        wait 1000

                        //dump all resources
                        if findtype 23035 ground as myStockpile
                            menu myStockpile 0
                            waitfortarget 1000
                            target 'self'
                        endif    

                        #drop gold on vault
                        if findtype 41518 ground as myVault
                            while findtype 3821 self as 'myGold'
                                lift myGold 60000
                                drop '0x40820133' -1 -1 0 
                                wait 600
                            endwhile
                        endif
                        
                        #recycler for magic items
                        if findtype 11762 ground any any 4 as found                         
                            wait 500
                            menu found 1
                            wait 500
                            menu found 1
                        endif
                    #End weight check recall home routine              
                    else
                        #Go to next spot
                        overhead "Recall Now!"                        
                        if usingRuneBook = 1
							if not gumpexists 1551740969
								dclick runeBookFarm
								wait 200
							endif
                            gumpresponse rune 1551740969
                        else          
                            #rune tome recall  
							if not gumpexists 167090027
								dclick runeBookFarm
								wait 200
							endif    
                            gumpresponse rune 167090027
                            wait 500
                            if tomeSideSwitch = 0
                                gumpresponse 20 167090027
                                @setvar! tomeSideSwitch 1
                            else 
                                gumpresponse 10 167090027
                                @setvar! tomeSideSwitch 0
                            endif                               
                        endif
                    endif
                    for 3 
                        wait 900       
                        if followers > 1
                            say "All guard me"
                        endif   
                    endfor
                endwhile
            endif            
            replay            
        endfor        
    elseif not find lasttarget ground -1 -1 8
        overhead "More than 8 tiles, gtfo" 34
        @setvar! notSeen 1
        if autoBotEnabled = 1
            wait 500
            replay
        endif
        stop
    else
        overhead "● ▼▼ HERE ▼▼ ●" 88 lasttarget
        settimer 'atThisSpot' 0
    endif    
endif

@setvar myKillTarget lasttarget 

if not varexist myPreviousKillTarget
    @setvar myPreviousKillTarget 0     
endif

getlabel myKillTarget desc
overhead desc

if not varexist myEarthPet or dead myEarthPet
    overhead "Your pet is dead, demonic summon time..." 34
    if findtype "an ancient mummy" ground -1 -1 10 as maybePet
        overhead "Found a mate.. maybe?" 88
        if noto maybePet = "friend"
            @setvar! myEarthPet maybePet
            replay
        endif
    endif
else
    menu myEarthPet 2
    wft 500
    target myKillTarget
endif

if diffhits >= 30 and mana > 12
    while not targetexists
        if diffhits >= 60
            cast 'Heal'
        else
            cast 'Greater Heal'                
        endif
        wait 50
        if hp = maxhp
            hotkey '> Interrupt'
            break
        endif
    endwhile
    if targetexists
        target self
    endif
elseif blessYourself = 1 and str <= 100
    while not targetexists
        cast 'Bless'
        wait 50
        if diffhits >= 30
            hotkey '> Interrupt'
            break
        endif
    endwhile
    if targetexists
        target self
    endif
endif

if not listexists myCursedTargets
    createlist myCursedTargets
endif

if not listexists myManaDrainTargets
    createlist myManaDrainTargets
endif

if find myKillTarget -1 -1 12 as killy
    if myKillTarget = myPreviousKillTarget
        // do nothing
    else
        
        // Herding using crook
        if skill "Herding" > 0 and findtype 3713 backpack as jrook
            dclick jrook
            wft 500
            target killy
        endif

        if inlist myManaDrainTargets myKillTarget
            overhead "We already cursed this guy" 88
            // do nothing
        elseif castManadrain = 0
            // do nothing 
        else
            while not targetexists and not dead myKillTarget
                if diffhits < 30
                    cast 'mana drain'
                    wait 50
                elseif diffhits >= 30 
                    hotkey '> Interrupt'
                    break
                endif
            endwhile
            if targetexists
                target myKillTarget
                pushlist myManaDrainTargets myKillTarget
                settimer killTargetTimer 0
                wait 500
            endif
        endif   
        
        if insysmsg "Target cannot be seen." 
            overhead "CANNOT BE SEEN MOVE TO NEXT TARGET" 34
            @setvar! notSeen 1
            replay
        endif 

        if inlist myCursedTargets myKillTarget
            overhead "We already cursed this guy" 88
            // do nothing
        elseif castCurse = 0
            // do nothing
        else
            while not targetexists and not dead myKillTarget
                if diffhits < 30
                    cast 'Curse'
                    wait 50
                elseif diffhits >= 30 
                    hotkey '> Interrupt'
                    break
                endif
            endwhile
            if targetexists
                target myKillTarget
                pushlist myCursedTargets myKillTarget
                settimer killTargetTimer 0
                wait 500
            endif
        endif
        
        if insysmsg "Target cannot be seen." 
            overhead "CANNOT BE SEEN MOVE TO NEXT TARGET" 34
            @setvar! notSeen 1
            replay
        endif 

        @setvar myPreviousKillTarget myKillTarget
    endif
else
    overhead "Out of range" 34
endif



clearsysmsg 

say "all guard"

@setvar! isLethalPoison 0

while not dead myKillTarget and find myKillTarget -1 -1 12 as killy
    
    if followers < 4
        replay
    endif

    if insysmsg "Target cannot be seen." 
        overhead "CANNOT BE SEEN MOVE TO NEXT TARGET" 34
        @setvar! notSeen 1
        replay
    endif 
    
    if timer magicShieldTimer >= cdMagicShield and not findbuff "Magic Reflection"
        cast "Magic Reflection"
        wait 1000
    elseif not findbuff "Magic Reflection" and not findbuff "Magic Resist Potion" and findtype "Black Potion" backpack as pot
        dclick pot
        wait 200
    endif

    if findbuff "Magic Reflection"
        settimer magicShieldTimer 0
    endif
    
    if timer reactiveArmorTimer >= cdReactiveArmor and not findbuff "Reactive Armor"
        cast "Reactive Armor"
        wait 200
    endif

    if findbuff "Reactive Armor"
        settimer reactiveArmorTimer 0
    endif
    if timer corpseSkinTimer >= cdCorpseSkin and skill "Necromancy" >= 65 and timer meditiationTimer >= cdMeditationMini
        overhead "Should be CorpseSkin!" 34
        say "[CorpseSkin"
        settimer corpseSkinTimer 0
        cooldown CorpseSkin cdCorpseSkin
        wait 500
    endif
    
    #provocation buff   
    if  skill "Provocation" > 50 and not findbuff "Song Of Provocation"
        overhead "Provocation Buff"
        while not targetexists 
            skill "Provocation"
            wait 200
        endwhile
        target backpack
        settimer discoTimer 0
    endif    
    #disco routine      
    if  discordOn = 0 and skill "Discordance" > 50 and timer "discoTimer" > 5050 and timer meditiationTimer >= cdMeditationMini
        if not findbuff "Song Of Discordance"
            overhead "Disco Buff"
            while not targetexists 
                skill "Discordance"
                wait 200
            endwhile
            target backpack
        endif
        overhead "Disco Target!" 33
        while not targetexists
            if dead myKillTarget or diffhits >= 30
                hotkey '> Interrupt'
                break
            else
                skill "Discordance" 
                wait 50
            endif 
        endwhile
        if targetexists
            target myKillTarget
            settimer discoTimer 0
        endif
        wait 250
        if insysmsg "You play successfully" 
            #this is reset after a new target is acquired 
            @setvar! discordOn 1
        endif
    endif
    
    if skill "poisoning" >= 100 or castPoisonAnyway = 1 and timer meditiationTimer >= cdMeditationMini

        if timer mindRotTimer >= cdMindRot and skill "Necromancy" >= 75 
            overhead "Should be mind rotting!" 34
            say "[MindRot"
            settimer mindRotTimer 0
            wait 500
        endif
        
        while isLethalPoison = 0
            
            if insysmsg "Target cannot be seen." 
                overhead "CANNOT BE SEEN MOVE TO NEXT TARGET" 34
                replay
            endif 
        
            getlabel myKillTarget desc2
            overhead desc2 66
            // poison lethal
            if "Lethal" in desc2
                @setvar! isLethalPoison 1
                break
            elseif deadlyOK = 1 and "Deadly" in desc2
                @setvar! isLethalPoison 1
                break
            elseif castPoisonAnyway = 1 and "Ticks" in desc2
                @setvar! isLethalPoison 1
                break
            elseif castPoisonAnyway = 1 and "Poison" in desc2
                @setvar! isLethalPoison 1
                break
            elseif diffhits >= 30 
                // need a heal
                break
            elseif mana >= 9
                while not targetexists
                    cast 'poison'
                    wait 50
                    if dead myKillTarget
                        hotkey '> Interrupt'
                        replay
                    endif
                endwhile
                target myKillTarget
                wait 200
                if insysmsg "Target cannot be seen." 
                     overhead "CANNOT BE SEEN MOVE TO NEXT TARGET" 34
                     @setvar! isLethalPoison 1
                endif 
            else
                wait 200
            endif
        endwhile
    endif
    
    
    #overhead "at this spot: {atThisSpot} poisonStrikeTimer: {poisonStrikeTimer}"
    if timer "poisonStrikeTimer" > 35000 and skill "Poisoning" > 50 timer "atThisSpot" > 8000   and timer meditiationTimer >= cdMeditationMini
            #try to poison strike
            say "[poisonStrike"
            wft 500
            target myKillTarget
            wait 100
            if insysmsg "You do not have enough unholy symbols for that"
                settimer "poisonStrikeTimer" 30000
            else
                settimer "poisonStrikeTimer" 0
            endif
    endif
    
    if timer evilOmenTimer >= cdEvilOmen and skill "Necromancy" >= 60 and timer meditiationTimer >= cdMeditationMini
        say "[EvilOmen"
        settimer evilOmenTimer 0
        cooldown EvilOmen cdEvilOmen
        wait 200
    endif

    if timer vampiricEmbraceTimer >= cdVampiricEmbrace and skill "Necromancy" >= 75 and timer meditiationTimer >= cdMeditationMini and findtype 8198 ground -1 -1 4 as jorpse
        say "[VampiricEmbrace"
        wft 500
        target jorpse
        settimer vampiricEmbraceTimer 0
        cooldown VampiricEmbrace cdVampiricEmbrace
    endif
   
    if find myKillTarget -1 -1 12 as killy
       // do nothing
    else
        overhead "Out of range" 34
        break
    endif
    
    if poisoned
        if findtype "Orange Potion" backpack as pot 
            dclick pot
            wait 200
        else
            while not targetexists
                if not poisoned
                    hotkey '> Interrupt'
                    break
                else
                    cast 'Cure'
                    wait 50
                endif 
            endwhile
            if targetexists
                target self
                wait 500
            endif
        endif
    endif

    if castLightning = 1 and diffhits < 30 and mana >= 12 and timer lightningTimer >= cdLightning and timer meditiationTimer >= cdMeditationMini
        while not targetexists
            if dead myKillTarget or diffhits >= 30
                hotkey '> Interrupt'
                break
            else
                cast 'Lightning'
                wait 50
            endif 
        endwhile
        if targetexists
            target myKillTarget
            settimer lightningTimer 0
            cooldown Lightning cdLightning
            wait 500
        endif
    endif
  
    if castMagicArrow = 1 and diffhits < 30 and mana >= 5 and timer magicArrowTimer >= cdMagicArrow and timer meditiationTimer >= cdMeditationMini
        while not targetexists
            if dead myKillTarget or diffhits >= 30
                hotkey '> Interrupt'
                break
            else
                cast 'magic arrow'
                wait 50
            endif 
        endwhile
        if targetexists
            target myKillTarget
            settimer magicArrowTimer 0
            cooldown MagicArrow cdMagicArrow
            wait 500
        endif
    endif

    if castHarm = 1 and diffhits < 30 and mana >= 9 and timer harmTimer >= cdHarm and timer meditiationTimer >= cdMeditationMini
        while not targetexists
            if dead myKillTarget or diffhits >= 30
                hotkey '> Interrupt'
                break
            else
                cast 'harm'
                wait 50
            endif 
        endwhile
        if targetexists
            target myKillTarget
            settimer harmTimer 0
            cooldown Harm cdHarm
            wait 500
        endif
    endif
    

    if castFireball = 1 anddiffhits < 30 and mana >= 12 and timer fireballTimer >= cdFireball and timer meditiationTimer >= cdMeditationMini
        while not targetexists
            if dead myKillTarget or diffhits >= 30
                hotkey '> Interrupt'
                break
            else
                cast 'fireball'
                wait 50
            endif 
        endwhile
        if targetexists
            target myKillTarget
            settimer fireballTimer 0
            cooldown Fireball cdFireball
            wait 500
        endif
    endif
    
    if diffhits >= 30
        if diffhits >= 65    
            if findtype "Yellow Potion" backpack as pot 
                getlabel pot desc
                if "next usable" in desc
                    // do nothing
                else
                    dclick pot
                    wait 200
                endif
            endif
        endif
        
        if diffhits >= 30 and mana >= 12
            while not targetexists
                if diffhits >= 60
                    cast 'Heal'
                else
                    cast 'Greater Heal'                
                endif
                wait 50
                if hp = maxhp
                    hotkey '> Interrupt'
                    break
                endif
            endwhile
            if targetexists
                target self
                wait 50
            endif
        endif
    elseif mana < 55 and findbuff "actively meditating" 
        // wait for mana
    elseif timer meditiationTimer < cdMeditationMini and findbuff "actively meditating" 
        // wait for mana
    elseif spamMindBlastOverFlamestrike = 1 mana > 42
        while not targetexists and mana > 40
            if dead myKillTarget or diffhits >= 30
                hotkey '> Interrupt'
                break
            else
                cast 'Mind Blast'
                wait 50
            endif 
        endwhile
        if targetexists
            target myKillTarget
            wait 500
        endif
    elseif mana > 42
        while not targetexists and mana > 65
            if dead myKillTarget or diffhits >= 30
                hotkey '> Interrupt'
                break
            else
                cast 'Flamestrike'
                wait 50
            endif 
        endwhile
        if targetexists
            target myKillTarget
            wait 500
        endif       
    elseif not findbuff "actively meditating" and timer magicMushroomTimer >= cdMushroom and findtype "mushroom" backpack as mushy
        overhead "Eatting shrooms" 66
        dclick mushy
        settimer magicMushroomTimer 0
    elseif not findbuff "actively meditating" and timer meditiationTimer >= cdMeditation
        useskill 'meditation'
        settimer meditiationTimer 0
        wait 500
    endif
    wait 50
endwhile
wait 500
if targetexists 
    hotkey 'Cancel Current Target'
endif
say "all guard me"
if canYouMakeAMushroom = 1 and counttype "mushroom" backpack < 5
    while counttype "mushroom" backpack < 15
        cast "Create Food"
        wait 200
        if counttype "mushroom" backpack = 0
            break
        endif
    endwhile
endif
while mana < 65
    if diffhits >= 30
        if diffhits >= 65    
            if findtype "Yellow Potion" backpack as pot 
                getlabel pot desc
                if "next usable" in desc
                    // do nothing
                else
                    dclick pot
                    wait 200
                endif
            endif
        endif

        if diffhits >= 30 and mana >= 12
            while not targetexists
                if diffhits >= 60
                    cast 'Heal'
                else
                    cast 'Greater Heal'                
                endif
                wait 50
                if hp = maxhp
                    hotkey '> Interrupt'
                    break
                endif
            endwhile
            if targetexists
                target self
                wait 50
            endif
        endif
    endif
    if not findbuff "actively meditating" 
        useskill 'meditation'
    endif
    wait 500
endwhile
if timer vampiricEmbraceTimer >= cdVampiricEmbrace and skill "Necromancy" >= 70 and findtype 8198 ground -1 -1 4 as jorpse
    say "[VampiricEmbrace"
    wft 500
    target jorpse
    settimer vampiricEmbraceTimer 0
endif

if timer killTargetTimer >= cdKillTarget
    overhead "We cleared curse and mana drain targets" 34
    removelist myCursedTargets      
    removelist myManaDrainTargets
endif
   
if autoBotEnabled = 1
    replay
endif